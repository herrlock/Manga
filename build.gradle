plugins {
	id 'org.ajoberstar.grgit' version '1.5.0'
}
ext {
	jfxJavadoc = project.hasProperty('jfx_doc_location') ? 
		'file:/' + project.getProperty('jfx_doc_location') :
		'http://docs.oracle.com/javafx/2/api/'
	if(!project.hasProperty('jfx_doc_location')) {
		logger.info('JavaFX-Documentation is set to http://docs.oracle.com/.' + 
			' If you want to specify a different location add \'jfx_doc_location\' to your gradle.properties')
	}
	jfxSource = project.hasProperty('jfx_src_location') ? 
		project.getProperty('jfx_src_location') :
		''
	if(!project.hasProperty('jfx_src_location')) {
		logger.info('JavaFX-Sources are not set.' + 
			' If you want to add it add \'jfx_src_location\' to your gradle.properties')
	}
}

apply plugin: 'distribution'

description = 'The root-project. Contains the subprojects'

// general setup
allprojects {

	group 'de.herrlock.manga'
	version '1.5.0-BETA-SNAPSHOT'

	apply plugin: 'eclipse'

	eclipse {
		project {
			if(rootProject.hasProperty('eclipseNamePrefix')) {
				name = rootProject.eclipseNamePrefix + '-' + project.name
			}
		}
	}
}

subprojects {
	
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'findbugs'
	apply plugin: 'pmd'
	apply plugin: 'jacoco'
	apply plugin: 'project-report'
	apply plugin: 'build-dashboard'
	
	sourceCompatibility = '1.7'
	
	dependencies {
		// compile-annotations
		compileOnly 'com.google.code.findbugs:annotations:3.0.1'
		
		// for testing
		testImplementation 'junit:junit:4.12'
	}
	
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		// options.javaHome = 
	}
	
	jar.manifest {
		attributes 'Built-At' : new Date(),
			'Built-With' : GradleVersion.current(),
			'Created-By' : "${System.properties.'java.runtime.version'} (${System.properties.'java.vm.vendor'})",
			'Version' : project.version,
			'Info'
		attributes 'Branch' : grgit.branch.getCurrent().getName(),
			'Commit' : grgit.head().getId(),
			'Date' : grgit.head().getDate(),
			'Git'
	}
	
	findbugs {
		effort = 'max'
		reportLevel = 'low'
	}
	tasks.withType(FindBugs) {
		reports {
			xml.enabled = false
			html.enabled = true
		}
	}
	
	pmd {
		ruleSetFiles = files("${rootProject.projectDir}/misc/pmd.rulesets.xml")
	}
	
	eclipse {
		project {
			buildCommand 'edu.umd.cs.findbugs.plugin.eclipse.findbugsBuilder'
		}
		classpath {
			file.withXml {
				def jfxrt = it.asNode().children().find{ entry ->
					return entry['@path'].contains('jfxrt') 
				}
				if(jfxrt != null) {
					if(jfxSource != '') {
						jfxrt.'@sourcepath' = jfxSource
					}
					Node attributes = new Node(jfxrt, 'attributes')
					Node attribute = new Node(attributes, 'attribute')
					attribute.'@name' = 'javadoc_location'
					attribute.'@value' = jfxJavadoc
				}
			}
		}
	}
	
	task sourceJar(type: Jar) {
		from sourceSets.main.allJava
		classifier 'sources'
	}
	assemble.dependsOn sourceJar
	
	task javadocJar(type: Jar, dependsOn: javadoc) {
		from tasks.javadoc.outputs
		classifier 'javadoc'
	}
	assemble.dependsOn javadocJar
	
	repositories {
		mavenCentral()
	}
}

// evaluate subprojects
project.evaluationDependsOnChildren()

// configure root-project

task dist(dependsOn: [assembleDist], group: 'Distribution', description: 'Run distZip and distTar') {
	// does nothing itself
}

task packDependencies {
	subprojects.each {
		dependsOn it.jar
	}

	File targetFolder = new File(rootProject.buildDir, 'tmp/packJars/')
	doLast {
		targetFolder.mkdirs()
	}
	java.util.jar.Pack200.Packer packer = java.util.jar.Pack200.newPacker();
	// pack with low compression rate, when is is requested or a name-prefix is present; the latter always indicates a development-version
	boolean lowEffortPacking = project.hasProperty('lep') || project.hasProperty('lowEffortPacking') || project.hasProperty('eclipseNamePrefix')
	packer.properties().putAll([
		(packer.EFFORT) : lowEffortPacking ? '0' : '9',
		(packer.SEGMENT_LIMIT) : '-1',
		(packer.KEEP_FILE_ORDER) : packer.FALSE,
		(packer.MODIFICATION_TIME) : packer.LATEST,
		(packer.DEFLATE_HINT) : packer.TRUE,
		(packer.UNKNOWN_ATTRIBUTE) : packer.ERROR
	])
	if(project.hasProperty('stripLNT')) {
		packer.properties().put(packer.CODE_ATTRIBUTE_PFX + 'LineNumberTable', packer.STRIP)
	}
	project(':MangaLauncher').configurations.runtimeClasspath.each { jar ->
		if(!'jfxrt.jar'.equals(jar.getName())) {
			inputs.file jar
			File newFile = new File(targetFolder, jar.getName() + '.pack.gz')
			outputs.file newFile
			doLast {
				new java.util.jar.JarInputStream( new FileInputStream(jar) ).withStream { jarIn ->
					new java.util.zip.GZIPOutputStream( newFile.newOutputStream() ).withStream { gzOut ->
						logger.info "Packing to ${newFile}"
						packer.pack( jarIn, gzOut );
					}
				}
			}
		}
	}
}

distributions {
	main {
		baseName = project.name
		contents {
			from 'LICENSE'
			from (project(':MangaLauncher').tasks.jar) {
				rename '.+', 'MangaLauncher.jar'
			}
			into ('licenses') {
				from 'licenses'
			}
			into('lib') {
				from packDependencies
			}
			filesMatching ('MangaLauncher.sh') {
				it.setMode 0744
			}
		}
	}
}

tasks.withType(Tar) {
	compression = Compression.GZIP
}

void assertPmdErrors(Task pmdTask, List expected) {
	assertPmdErrors pmdTask.reports.xml.destination, expected
}

void assertPmdErrors(File file, List expected) {
	def violations = new XmlSlurper().parse(file).file.violation
	assert violations.size() == expected.size(), 
		"Wrong number of PMD warnings: Expected ${expected.size()} violations but found ${violations.size()}. See the report at: ${file.toURI()}"
	violations.eachWithIndex { element,index ->
		def exp = expected.get(index)
		assert "${element.@package}.${element.@class}" == exp.clazz, "Wrong class. Is ${element.@package}.${element.@class} but should be ${exp.clazz}"
		assert element.@method == exp.method, "Wrong method. Is ${element.@method} but should be ${exp.method}"
		assert element.@rule == exp.rule, "Wrong PMD rule. Is ${element.@rule} but should be ${exp.rule}"
	}
	println 'Suppressing PMD warnings, as they seem to be expected.'
}
